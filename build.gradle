buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'

configure(allprojects) { project ->
    group 'org.samsara'
    version '2.0.0-ALPHA1'

    apply plugin: 'java'
    apply plugin: 'idea'

    compileJava {
        targetCompatibility = 1.8
        sourceCompatibility = 1.8
    }


    repositories {
        jcenter()
    }
}

configure(subprojects.findAll {!it.name.contains("common")}) { project ->
    apply plugin: 'spring-boot'

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Brixton.SR5'
        }
    }

    ext {
        mysqlConnectorVersion = '6.0.3'
    }

    compileJava {
        targetCompatibility = 1.8
        sourceCompatibility = 1.8
    }

    repositories {
        jcenter()
    }

    dependencies {
        compile "org.springframework.boot:spring-boot-starter-actuator"

        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

project("config-service") {
    dependencies {
        compile "org.springframework.cloud:spring-cloud-config-server"

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}

project("discovery-service") {
    dependencies {
        compile "org.springframework.cloud:spring-cloud-starter-config"
        compile "org.springframework.cloud:spring-cloud-starter-eureka-server"

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}

project("api-gateway") {
    dependencies {
        compile "org.springframework.cloud:spring-cloud-starter-config"
        compile "org.springframework.cloud:spring-cloud-starter-eureka"
        compile "org.springframework.cloud:spring-cloud-starter-hystrix"
        compile "org.springframework.cloud:spring-cloud-starter-zuul"
        compile "org.springframework.cloud:spring-cloud-starter-oauth2"

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}

project("user-service") {
    dependencies {
        // Spring Cloud dependencies
        compile "org.springframework.cloud:spring-cloud-starter-config"
        compile "org.springframework.cloud:spring-cloud-starter-eureka"
        compile "org.springframework.cloud:spring-cloud-starter-oauth2"

        // Spring Boot dependencies
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "org.springframework.boot:spring-boot-starter-data-rest"

        // Database dependencies
        compile "mysql:mysql-connector-java:${mysqlConnectorVersion}"
    }
}

project("book-service") {
    dependencies {
        compile "org.springframework.cloud:spring-cloud-starter-eureka"
        compile "org.springframework.boot:spring-boot-starter-data-jpa"
        compile "org.springframework.boot:spring-boot-starter-data-rest"
        compile "mysql:mysql-connector-java:${mysqlConnectorVersion}"
    }
}

project("movie-service") {
    dependencies {

    }
}

project("rating-service") {
    dependencies {

    }
}

project("recommendation-service") {
    dependencies {

    }
}

project("pistachio-web-ui") {
    dependencies {

    }
}

project("microservice-common") {
    dependencies {

    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}

